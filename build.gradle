plugins {
    id 'java-library'
    id 'maven-publish'
    id 'eclipse'
    id 'org.gradlex.extra-java-module-info' version '1.4.2'
    id 'net.minecraftforge.gradleutils' version '2.+'
}

version = gradleutils.getTagOffsetVersion()
logger.lifecycle('Version: ' + version)

java {
    toolchain.languageVersion = JavaLanguageVersion.of(16)
    modularity.inferModulePath.set(true)
    withSourcesJar()
}

repositories {
    mavenCentral()
    maven gradleutils.forgeMaven
}

dependencies {
    api(libs.bundles.asm)
    implementation(libs.unsafe)
}

extraJavaModuleInfo {
    failOnMissingModuleInfo = false
}

changelog {
    fromTag '0.9'
}

jar {
    manifest {
        attributes([
            'Specification-Title': 'securejarhandler',
            'Specification-Vendor': 'forge',
            'Specification-Version': gradleutils.gitInfo.tag,
            'Implementation-Title': project.name,
            'Implementation-Version': project.version,
            'Implementation-Vendor':'forge'
        ] as LinkedHashMap)
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            pom {
                name = 'Secure Modular Jar handler'
                description = 'Making the Java modular system provide security information'
            }
        }
    }
    repositories {
        maven gradleutils.getPublishingForgeMaven()
    }
}
    
// Hack eclipse into knowing that the gradle deps are modules
eclipse.classpath {
    containers 'org.eclipse.buildship.core.gradleclasspathcontainer'
    file.whenMerged { entries.findAll { it.kind == 'lib' || it.path == 'org.eclipse.buildship.core.gradleclasspathcontainer' }.each { it.entryAttributes['module'] = 'true' } }
}

allprojects {
    ext.VALID_VMS = [
        'Adoptium':  [16, 17, 18, 19, 20, 21],
        'Amazon':    [16, 17, 18, 19, 20, 21],
        'Azul':      (16..21),
        'BellSoft':  (16..21),
        'Graal_VM':  [16, 17,     19, 20, 21],
        'IBM':       [16, 17, 18, 19, 20    ],
        'Microsoft': [16, 17,             21],
        'Oracle':    (16..21),
        'SAP':       (16..20)
    ]
    //ext.VALID_VMS = [ 'Adoptium':  [16] ]
}